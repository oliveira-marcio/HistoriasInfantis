apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdk 34
    defaultConfig {
        applicationId "com.abobrinha.caixinha"
        minSdk 21
        targetSdk 34
        versionCode 27
        versionName "3.1.1"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    flavorDimensions = ["tier"]
    productFlavors{
        free {
            dimension "tier"
            applicationId "com.abobrinha.caixinha"
        }
        paid {
            dimension "tier"
            applicationId "com.abobrinha.caixinha"
        }
    }
    namespace 'com.abobrinha.caixinha'
}

ext {
    appCompatLibVersion = '1.6.1'
    cardViewLibVersion = '1.0.0'
    constraintLayoutLibVersion = '2.1.4'
    firebaseJobDispatcherLibVersion = '0.8.5'
    firebaseLibVersion = '23.3.1'
    firebaseAdsVersion = '19.3.0'
    glideLibVersion = '3.8.0'
    vectorDrawableVersion = '1.1.0'
    jetpackLibVersion = '1.7.0'
    recyclerViewVersion = '1.3.2'
    jsoupLibVersion = '1.13.1'
    legacySupportLibVersion = '1.0.0'
    materialLibVersion = '1.10.0'
    multiDexLibVersion = '2.0.1'
    preferenceLibVersion = '1.2.1'
    espressoCoreLibVersion = '3.1.0'
    espressoIntentsLibVersion = '3.5.1'
    junitLibVersion = '4.13.2'
    junitExtLibVersion = '1.1.5'
    testRulesLibVersion = '1.5.0'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:${appCompatLibVersion}"
    implementation "androidx.legacy:legacy-support-v4:${legacySupportLibVersion}"
    implementation "androidx.vectordrawable:vectordrawable:${vectorDrawableVersion}"
    implementation "androidx.vectordrawable:vectordrawable-animated:${vectorDrawableVersion}"
    implementation "com.google.android.material:material:${materialLibVersion}"
    implementation "androidx.annotation:annotation:${jetpackLibVersion}"
    implementation "androidx.recyclerview:recyclerview:${recyclerViewVersion}"
    implementation "androidx.cardview:cardview:${cardViewLibVersion}"
    implementation "androidx.preference:preference:${preferenceLibVersion}"
    implementation "androidx.constraintlayout:constraintlayout:${constraintLayoutLibVersion}"
    implementation "androidx.multidex:multidex:${multiDexLibVersion}"
    implementation "com.github.bumptech.glide:glide:${glideLibVersion}"
    implementation "org.jsoup:jsoup:${jsoupLibVersion}"
    implementation "com.firebase:firebase-jobdispatcher:${firebaseJobDispatcherLibVersion}"
    implementation "com.google.firebase:firebase-messaging:${firebaseLibVersion}"
    implementation "androidx.test.espresso:espresso-idling-resource:${espressoIntentsLibVersion}"

    freeImplementation "com.google.firebase:firebase-ads:${firebaseAdsVersion}"
    testImplementation "junit:junit:${junitLibVersion}"

    androidTestImplementation "androidx.annotation:annotation:${jetpackLibVersion}"
    androidTestImplementation("androidx.test.espresso:espresso-core:${espressoCoreLibVersion}", {
        exclude group: "com.android.support", module: "support-annotations"
    })
    androidTestImplementation "androidx.test.espresso:espresso-intents:${espressoIntentsLibVersion}"
    androidTestImplementation "androidx.test:rules:${testRulesLibVersion}"
    androidTestImplementation "androidx.test.ext:junit:${junitExtLibVersion}"
}